plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.diffplug.spotless" version "5.16.0"
}

// this groupId is OSSRH registered https://issues.sonatype.org/browse/OSSRH-76799
group 'io.github.markrmullan'
version '1.0.0-SNAPSHOT'

task printVersion {
    println project.version
}

def isReleaseBuild() {
    return !project.version.contains("SNAPSHOT")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.github.com/en/actions/publishing-packages/publishing-java-packages-with-gradle
    // and https://docs.gradle.org/current/userguide/publishing_setup.html
    repositories {
        maven {
            name = "OSSRH"
            url = isReleaseBuild() ? "https://s01.oss.sonatype.org/content/repositories/releases/" : "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}